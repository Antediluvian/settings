/*
 * Copyright (C) 2014 China Telecom System Integration Co., Ltd.
 * All rights reserved.
 */

ext {
    /**
     * Builds an Android version code from the version of the project.
     * @return the version code
     */
    buildVersionCode = {
        def (major, minor, patch) = version.version.tokenize('.');
        if (patch == null) {
            patch = 0;
        }

        (major, minor, patch) = [major, minor, patch].collect {
            it.toInteger();
        }

        int b = version.build.toInteger();
        if (b == 0) {
            b = 999;
        }

        (major * 1000000) + (minor * 100000) + (patch * 10000) + b;
    }

    /**
     * Builds an Android version name from the version of the project.
     * @return the version name
     */
    buildVersionName = {
        def rtn = version.version;
        if (version.build == '0') {
            rtn += ' (' + version.suffix + ')';
        } else {
            rtn += ' (b' + version.build + ')';
        }

        rtn
    }
}
